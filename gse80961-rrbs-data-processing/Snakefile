import pandas as pd
from pathlib import Path

configfile: 'config.yaml'

include: 'rules/trim-galore.smk'
include: 'rules/bismark.smk'
include: 'rules/methyldackel.smk'
include: 'rules/sambamba.smk'
include: 'rules/download.smk'

manifest = pd.read_csv(config['manifest'])
DATA_DIR = Path(config['data_dir'])
RESULT_DIR = Path(config['result_dir'])

SE_SAMPLES = manifest[manifest.library_layout.str.upper().str.contains('SINGLE')].run_accession.values
PE_SAMPLES = manifest[manifest.library_layout.str.upper().str.contains('PAIRED')].run_accession.values
# print('There are %d single and %d paired-end samples.' % (len(SE_SAMPLES), len(PE_SAMPLES)))

SE_BEDGRAPHS = expand(str(RESULT_DIR / '03_methyldackel' / 'se' / '{sample}_CpG.sorted.bedGraph'), sample=SE_SAMPLES)
PE_BEDGRAPHS = expand(str(RESULT_DIR / '03_methyldackel' / 'pe' / '{sample}_CpG.sorted.bedGraph'), sample=PE_SAMPLES)

ALL = []
ALL.append(SE_BEDGRAPHS)
ALL.append(PE_BEDGRAPHS)

wildcard_constraints:
    sample = 'SRR[0-9]+'

rule all:
    input: ALL

rule clean:
    shell:
        "if [ -d {RESULT_DIR} ]; then rm -r {RESULT_DIR}; fi; "
        "if [ -d {DATA_DIR} ]; then rm -r {DATA_DIR}; fi; "
        "if [ -d logs ]; then rm -r logs; fi; "
        "if [ -d benchmarks ]; then rm -r benchmarks; fi; "

rule bedtools_sort:
    input:
        RESULT_DIR / '03_methyldackel' / 'pe' / '{sample}_CpG.bedGraph'
    output:
        RESULT_DIR / '03_methyldackel' / 'pe' / '{sample}_CpG.sorted.bedGraph'
    shell:
        'bedtools sort -i {input} > {output}'

